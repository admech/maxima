init() := block(
	[
		log : get_logger("plot", log_level_info)
	],

	defstruct(
		Plot( /* different ways of showing */
			devnull,
			list,
			windows,
			console,
			files_in
		)
	),
	plot : new(Plot),

	defstruct( /* a way of showing */
		Output(
			name, /* printed to log at pipeline launch */
			show  /* [sol, plot_groups] -> < present visually > */
		)
	),

	defstruct( /* a figure */
		PlotGroup(
			name,     /* plot title */
			vars,     /* list of PlotVars */
			postamble /* e.g. scale axes equally */
		)
	),

	defstruct( /* a line on a figure */
		PlotVar(
			name, /* shown in legend */
			get   /* [p] -> [ absciss, ordinate ] */
		)
	),

	/* show nothing */
	plot@devnull : new(Output(
		"sending to /dev/null",
		buildq([log],
			lambda([sol, plot_groups],
				log@info("plot > /dev/null")
			)
		)
	)),

	/* print all points of the solution */
	plot@list : new(Output(
		"listing the solution into the console",
		buildq([log],
			lambda([sol, plot_groups],
				block(
					log@info("Solution:"),
					for p in sol do log@info(p),
					log@info("End of solution.")
				)
			)
		)
	)),

	/* QT windows */
	plot@windows : new(Output(
		"plotting in windows",
		buildq([log],
			lambda([sol, plot_groups],
				plot_groups_separately(
					"Gnuplotting to QT window...", /* FOR SOME REASON ONLY THE LAST WINDOW STICKS...*/
					window_term
				)
			)
		)
	)),
	window_num : 0,
	next_window_num() := (window_num: window_num + 1),
	window_term(group) := 
		[ gnuplot_preamble, sconcat(
				"set terminal qt ", next_window_num(),
				" persist",
				" title '", group@name, "'"
		) ]
	,

	/* ASCII art in the console */
	plot@console : new(Output(
		"plotting in the console",
		buildq([log, console_term],
			lambda([sol, plot_groups],
				plot_groups_separately(
					"Gnuplotting to console...", 
					console_term
				)
			)
		)
	)),
	console_term(group) := [ gnuplot_term, dumb ],

	/* file */
	file_term(target_dir) := buildq([target_dir], lambda([group], block([target_file],
		target_file : concat(target_dir, "/", group@name, ".eps"),
		log@info("[ plot ] Plotting", group@name, "to", target_file),
		set_plot_option([gnuplot_out_file, target_file]),
		[ gnuplot_term, ps ]
	))),
	plot@files_in : buildq([target_dir, file_term], lambda([target_dir], new(Output(
		"plotting to files",
		buildq([log, target_dir],
			lambda([sol, plot_groups],
				plot_groups_separately(
					"Gnuplotting to files...", 
					file_term(target_dir)
				)
			)
		)
	)))),

	/*
		FUNTIONS THAT DO THE ACTUAL PLOTTING
	*/

	plot_groups_separately(msg, term) := block(
		log@info(msg),
		for group in plot_groups do
			plot_to_terminal(
				sol, group@vars,
				term(group),
				group@postamble
			)
	),

	plot_to_terminal : buildq([log], lambda([sol, plot_vars, terminal, postamble],
		log@debug(terminal),
		plot2d (
			makelist(
				[ discrete, makelist(var@get(p), p, sol) ],
				var, plot_vars
			),
			cons(
				legend,
				makelist(var@name, var, plot_vars)
			),
			terminal,
			postamble
		)
	)),

	/* POSTAMBLES */
	no_postamble : [ gnuplot_postamble, "" ],
	equal_axes   : [ gnuplot_postamble, "set size ratio -1;" ]

) $