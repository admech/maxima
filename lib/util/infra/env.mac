/*
	when launching from command line, these two are to be overridden in --batch-string
	example (current directory should be lib):
	maxima --batch-string="sol_dir: \"test\" $ root_dir: \"`pwd`/..\" $ batchload(\"main.mac\") $ quit()$"
*/
if not(member('root_dir, values)) then root_dir : "/Users/data/Yandex.Disk/work/aspa/zobova/maxima" $
if not(member('sol_dir,  values)) then sol_dir  : "smoke" $

lib_dir       : "lib" $
sol_base_dir  : "sol" $
sol_rel_dir   : concat(sol_base_dir, "/", sol_dir) $
in_dir        : concat(sol_rel_dir, "/in") $
out_dir       : concat(sol_rel_dir, "/out") $

linel           : 160 $
gnuplot_command : "/usr/local/bin/gnuplot" $
float2bf        : true $
ratprint        : false $
maxima_tempdir  : concat(root_dir, "/maxima_tempdir") $

/* SIMULATION PARAMETERS */

use_default_simulation_parameters() := block(
	[
		pairs: []
	],
	print("[PARAMS] USING DEFAULTS:"),
	for pair in DEFAULT_SIMULATION_PARAMETERS do /* these should be defined in main - prior to loading the model */
		pairs: append(pairs, [ map([s] -> strim(" ", s), pair) ]),
	map(print, pairs),
	SIMULATION_PARAMETERS: pairs 
) $ 

get_global(param) := eval_string(assoc(param, SIMULATION_PARAMETERS, UNDEFINED)) $
parse_kv(line) := map([s] -> strim(" ", s), split(line, "=")) $
load_simulation_parameters_from_file() := block(
	[
		path: concat(root_dir, "/", in_dir, "/", "simulation_parameters.mac"),
		s,
		lines: []
	],
	print("[PARAMS] LOADING FROM", path, ":"),
	s: openr(path),
	for line:readline(s) next readline(s) while not(line = false) do
		if not(line = "") then lines: append(lines, [ parse_kv(line) ]),
	close(s),
	print("[PARAMS] LOADED:"),
	map(print, lines),
	SIMULATION_PARAMETERS: lines
) $ 

initialize_simulation_parameters() := error("DEFINE initialize_simulation_parameters IN main.mac FOR YOUR SIMULATION.") $