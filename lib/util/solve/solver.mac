require@logging() $
require@timing() $
require@model() $

init() := block(
	[
		batch_solve
	],
	solog : get_logger("solver", log_level_info),

	defstruct(
		Solver(
			simple, /* [Model] -> sol */
			batch
		)
	),
	solver : new(Solver),

	solver@simple : simulate,

	batch_solve(batches, solver) := buildq([batches, solver], lambda([model], block(
		[
			sol: [],
			last_point : [],
			/* first, store the intials and time range */
			initials : model@rk_adapter@initials,
			domain : model@rk_adapter@domain
		],
		for batch in batches do (
			solog@info("Solving batch", batch),
			domain@finish : batch, /* then set t1 to the batch, but leave initials and t0 as is */

			timeit_start(),
			sol_batch : solve_with(model, initials, domain, solver),
			solog@info("Solved batch", batch),
			timeit_show(),
			save_sol(sol_batch, concat("batch_", ssubst("_", ".", string(batch)))),

			/* after obtaining the solution for the batch, take next initials and t0 from its last point */
			last_point : last(sol_batch),
			domain@start : first(last_point),
			initials : rest(last_point, 1),

			/* last, add the solution for the batch to the full solution, */
			/* while dropping the last point as it will be the initial for the next batch */
			solog@info("Appending batch", batch),
			sol : append(sol, rest(sol_batch, -1)),
			solog@info("Appended batch", batch)
		),
		sol : append(sol, [ last_point ]) /* finally, restore the last point as there are no more next batches */
	))),
	solver@batch : batch_solve

) $
